apply plugin: 'com.android.application'

// Create a variable called keystorePropertiesFile, and initialize it to your
// keystore.properties file, in the rootProject folder.
def keystorePropertiesFile = rootProject.file("keystore.properties")

// Initialize a new Properties() object called keystoreProperties.
def keystoreProperties = new Properties()

// Load your keystore.properties file into the keystoreProperties object.
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdk 34
    defaultConfig {
        applicationId "com.yumesoftworks.fileshare"
        minSdkVersion 19
        targetSdkVersion 34
        multiDexEnabled true
        versionCode 10
        versionName "1.0.9"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    signingConfigs{
        configFree{
            keyAlias keystoreProperties['keyAliasFree']
            keyPassword keystoreProperties['keyPasswordFree']
            storeFile file(keystoreProperties['storeFileFree'])
            storePassword keystoreProperties['storePasswordFree']
        }
        configPaid{
            keyAlias keystoreProperties['keyAliasPaid']
            keyPassword keystoreProperties['keyPasswordPaid']
            storeFile file(keystoreProperties['storeFilePaid'])
            storePassword keystoreProperties['storePasswordPaid']
        }
    }

    flavorDimensions = ["tier"]
    productFlavors {
        free {
            dimension "tier"
            applicationId 'com.yumesoftworks.fileshare.free'
            resValue "string", "app_name", "File Share"
        }

        paid {
            dimension "tier"
            applicationId 'com.yumesoftworks.fileshare.paid'
            resValue "string", "app_name", "File Share Pro"
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            productFlavors.free.signingConfig signingConfigs.configFree
            productFlavors.paid.signingConfig signingConfigs.configPaid
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    namespace 'com.yumesoftworks.fileshare'
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'androidx.appcompat:appcompat:1.6.1'

    implementation 'androidx.vectordrawable:vectordrawable-animated:1.1.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.6'
    implementation 'androidx.browser:browser:1.6.0'
    implementation 'androidx.media:media:1.6.0'

    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'
    implementation 'com.google.android.material:material:1.9.0'

    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test:core:1.5.0'

    debugImplementation 'androidx.fragment:fragment-testing:1.6.1'
    debugImplementation 'androidx.test:core:1.5.0'

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.5.1'

    testImplementation 'junit:junit:4.13.2'

    //glide
    implementation 'com.github.bumptech.glide:glide:4.11.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'

    //room
    implementation 'androidx.room:room-runtime:2.5.2'
    annotationProcessor 'androidx.room:room-compiler:2.5.2'

    //View model and lifecycle
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    annotationProcessor 'androidx.lifecycle:lifecycle-compiler:2.6.2'
    implementation 'androidx.lifecycle:lifecycle-viewmodel:2.6.2'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.6.2'

    //multidex
    implementation 'androidx.multidex:multidex:2.0.1'

    //firebase analytics and admob
    freeImplementation 'com.google.android.gms:play-services-ads:22.4.0'
    implementation platform('com.google.firebase:firebase-bom:32.2.0')

    // Recommended: Add the Firebase SDK for Google Analytics.
    implementation 'com.google.firebase:firebase-analytics'

    // Add the Firebase Crashlytics SDK.
    implementation 'com.google.firebase:firebase-crashlytics'

    //consent for gdrp
    implementation 'com.google.android.ads.consent:consent-library:1.0.8'

    constraints {
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk7:1.8.0") {
            because("kotlin-stdlib-jdk7 is now a part of kotlin-stdlib")
        }
        implementation("org.jetbrains.kotlin:kotlin-stdlib-jdk8:1.8.0") {
            because("kotlin-stdlib-jdk8 is now a part of kotlin-stdlib")
        }
    }
}

apply plugin: 'com.google.gms.google-services'
// Apply the Crashlytics Gradle plugin
apply plugin: 'com.google.firebase.crashlytics'